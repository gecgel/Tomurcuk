function(Tomurcuk_DefinePackage TARGET_NAME DIRECTORY)
    if(NOT TARGET_NAME)
        message(FATAL_ERROR "Tomurcuk_DefinePackage: provide a target name")
    endif()
    if(TARGET ${TARGET_NAME})
        message(FATAL_ERROR "Tomurcuk_DefinePackage: there is already a target named ${TARGET_NAME}")
    endif()

    if(NOT DIRECTORY)
        message(FATAL_ERROR "Tomurcuk_DefinePackage: provide a package directory")
    endif()
    if(NOT IS_DIRECTORY ${DIRECTORY})
        message(FATAL_ERROR "Tomurcuk_DefinePackage: ${DIRECTORY} is not a directory")
    endif()

    set(OPTIONS "")
    set(ONE_VALUE_ARGS "")
    set(MULTI_VALUE_ARGS PUBLIC_DEPENDENCIES PRIVATE_DEPENDENCIES CUSTOM_DEPENDENCIES)
    cmake_parse_arguments(ARG "${OPTIONS}" "${ONE_VALUE_ARGS}" "${MULTI_VALUE_ARGS}" ${ARGN}) # cSpell: disable-line
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "Tomurcuk_DefinePackage: unparsed arguments ${ARG_UNPARSED_ARGUMENTS}")
    endif()

    if(ARG_PUBLIC_DEPENDENCIES)
        foreach(DEPENDENCY ${ARG_PUBLIC_DEPENDENCIES})
            if(NOT TARGET ${DEPENDENCY})
                message(FATAL_ERROR "Tomurcuk_DefinePackage: public dependency ${DEPENDENCY} is not a target")
            endif()
        endforeach()
    endif()
    if(ARG_PRIVATE_DEPENDENCIES)
        foreach(DEPENDENCY ${ARG_PRIVATE_DEPENDENCIES})
            if(NOT TARGET ${DEPENDENCY})
                message(FATAL_ERROR "Tomurcuk_DefinePackage: private dependency ${DEPENDENCY} is not a target")
            endif()
        endforeach()
    endif()

    set(PUBLIC_DIRECTORY "${DIRECTORY}/public")
    set(PRIVATE_DIRECTORY "${DIRECTORY}/private")
    set(PLATFORM_DIRECTORY "${DIRECTORY}/${TOMURCUK_CURRENT_PLATFORM}")
    set(PLATFORM_PUBLIC_DIRECTORY "${PLATFORM_DIRECTORY}/public")
    set(PLATFORM_PRIVATE_DIRECTORY "${PLATFORM_DIRECTORY}/private")
    set(PUBLIC_INCLUDE_DIRECTORIES ${PUBLIC_DIRECTORY} ${PLATFORM_PUBLIC_DIRECTORY})
    set(PRIVATE_INCLUDE_DIRECTORIES ${PRIVATE_DIRECTORY} ${PLATFORM_PRIVATE_DIRECTORY})

    Tomurcuk_FindFiles(HEADER_FILES RECURSE EXTENSIONS ${TOMURCUK_HEADER_FILE_EXTENSIONS} DIRECTORIES
        ${PUBLIC_INCLUDE_DIRECTORIES}
        ${PRIVATE_INCLUDE_DIRECTORIES}
    )
    Tomurcuk_FindFiles(TRANSLATION_FILES RECURSE EXTENSIONS ${TOMURCUK_TRANSLATION_FILE_EXTENSIONS} DIRECTORIES
        ${PRIVATE_DIRECTORY}
        ${PLATFORM_PRIVATE_DIRECTORY}
    )

    if(IS_DIRECTORY ${PUBLIC_DIRECTORY} OR IS_DIRECTORY ${PLATFORM_PUBLIC_DIRECTORY} OR ARG_PUBLIC_DEPENDENCIES)
        set(HAS_PUBLIC_PARTS ON)
    endif()
    if(IS_DIRECTORY ${PRIVATE_DIRECTORY} OR IS_DIRECTORY ${PLATFORM_PRIVATE_DIRECTORY} OR ARG_PRIVATE_DEPENDENCIES)
        set(HAS_PRIVATE_PARTS ON)
    endif()

    if(NOT HAS_PUBLIC_PARTS AND HAS_PRIVATE_PARTS)
        add_executable(${TARGET_NAME} EXCLUDE_FROM_ALL ${HEADER_FILES} ${TRANSLATION_FILES})
        Tomurcuk_SetupCompilationFeatures(${TARGET_NAME})
        target_link_libraries(${TARGET_NAME} PRIVATE ${ARG_PRIVATE_DEPENDENCIES})
        foreach(INCLUDE_DIRECTORY ${PRIVATE_INCLUDE_DIRECTORIES})
            if(IS_DIRECTORY ${INCLUDE_DIRECTORY})
                target_include_directories(${TARGET_NAME} PRIVATE ${INCLUDE_DIRECTORY})
            endif()
        endforeach()
    elseif(HAS_PRIVATE_PARTS)
        add_library(${TARGET_NAME} EXCLUDE_FROM_ALL ${HEADER_FILES} ${TRANSLATION_FILES})
        Tomurcuk_SetupCompilationFeatures(${TARGET_NAME})
        target_link_libraries(${TARGET_NAME} PRIVATE ${ARG_PRIVATE_DEPENDENCIES} PUBLIC ${ARG_PUBLIC_DEPENDENCIES})
        foreach(INCLUDE_DIRECTORY ${PUBLIC_INCLUDE_DIRECTORIES})
            if(IS_DIRECTORY ${INCLUDE_DIRECTORY})
                target_include_directories(${TARGET_NAME} PUBLIC ${INCLUDE_DIRECTORY})
            endif()
        endforeach()
        foreach(INCLUDE_DIRECTORY ${PRIVATE_INCLUDE_DIRECTORIES})
            if(IS_DIRECTORY ${INCLUDE_DIRECTORY})
                target_include_directories(${TARGET_NAME} PRIVATE ${INCLUDE_DIRECTORY})
            endif()
        endforeach()
    else()
        add_library(${TARGET_NAME} INTERFACE EXCLUDE_FROM_ALL ${HEADER_FILES})
        target_link_libraries(${TARGET_NAME} INTERFACE ${ARG_PUBLIC_DEPENDENCIES})
        foreach(INCLUDE_DIRECTORY ${PUBLIC_INCLUDE_DIRECTORIES})
            if(IS_DIRECTORY ${INCLUDE_DIRECTORY})
                target_include_directories(${TARGET_NAME} INTERFACE ${INCLUDE_DIRECTORY})
            endif()
        endforeach()
    endif()

    if(ARG_CUSTOM_DEPENDENCIES)
        add_dependencies(${TARGET_NAME} ${ARG_CUSTOM_DEPENDENCIES})
    endif()
endfunction()
