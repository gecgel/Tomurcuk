set(TOMURCUK_HEADER_FILE_EXTENSIONS ".hpp" ".txt")
set(TOMURCUK_TRANSLATION_FILE_EXTENSIONS ".cpp")
message(STATUS "TOMURCUK_HEADER_FILE_EXTENSIONS: ${TOMURCUK_HEADER_FILE_EXTENSIONS}")
message(STATUS "TOMURCUK_TRANSLATION_FILE_EXTENSIONS: ${TOMURCUK_TRANSLATION_FILE_EXTENSIONS}")

function(Tomurcuk_FindFiles OUTPUT)
    set(OPTIONS RECURSE LIST_DIRECTORIES)
    set(ONE_VALUE_ARGS "")
    set(MULTI_VALUE_ARGS DIRECTORIES EXTENSIONS)
    cmake_parse_arguments(ARG "${OPTIONS}" "${ONE_VALUE_ARGS}" "${MULTI_VALUE_ARGS}" ${ARGN}) # cSpell: disable-line
    if(NOT ARG_DIRECTORIES)
        message(FATAL_ERROR "Tomurcuk_FindFiles: DIRECTORIES argument is required")
    endif()
    if(NOT ARG_EXTENSIONS)
        message(FATAL_ERROR "Tomurcuk_FindFiles: EXTENSIONS argument is required")
    endif()
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "Tomurcuk_FindFiles: unparsed arguments ${ARG_UNPARSED_ARGUMENTS}")
    endif()

    set(PATTERNS "")
    foreach(DIRECTORY ${ARG_DIRECTORIES})
        foreach(EXTENSION ${ARG_EXTENSIONS})
            if(ARG_RECURSE)
                list(APPEND PATTERNS "${DIRECTORY}/**${EXTENSIONS}")
            else()
                list(APPEND PATTERNS "${DIRECTORY}/*${EXTENSIONS}")
            endif()
        endforeach()
    endforeach()

    if(ARG_LIST_DIRECTORIES)
        set(LIST_DIRECTORIES_VALUE "true")
    else()
        set(LIST_DIRECTORIES_VALUE "false")
    endif()

    if(ARG_RECURSE)
        file(GLOB_RECURSE FOUND_FILES CONFIGURE_DEPENDS LIST_DIRECTORIES ${LIST_DIRECTORIES_VALUE} ${PATTERNS})
    else()
        file(GLOB FOUND_FILES CONFIGURE_DEPENDS LIST_DIRECTORIES ${LIST_DIRECTORIES_VALUE} ${PATTERNS})
    endif()

    set(${OUTPUT} ${FOUND_FILES} PARENT_SCOPE)
endfunction()
